; id: json
; version: https://www.rfc-editor.org/rfc/rfc7159.txt
; uses: core, number


json       = BLANK value BLANK

value      = false / null / true / object / array / number / string

false      = %x66.61.6C.73.65   ; false

null       = %x6E.75.6C.6C      ; null

true       = %x74.72.75.65      ; true


object     = BLANK "{" BLANK [ member *( BLANK "," BLANK member ) ] BLANK "}" BLANK

member     = string BLANK ":" BLANK value

array      = BLANK "[" BLANK [ value *( BLANK "," BLANK value ) ] BLANK "]" BLANK



string     = DQUOTE *(escaped / unescaped) DQUOTE

escaped    = BCKSLASH (       ; after '\' character, 
               DQUOTE / 
               BCKSLASH / 
               SLASH /
               "b" /          ; 'b' represents BCKSP,
               "f" /          ; 'f' represents FF,
               "n" /          ; 'n' represents LF,
               "r" /          ; 'r' represents CR,
               "t" /          ; 't' represents HTAB,
               "u" 4HEXDIG    ; "uXXXX" represents a char of code U+XXXX
             )

unescaped  = %x20-21 / %x23-5B / %x5D-10FFFF